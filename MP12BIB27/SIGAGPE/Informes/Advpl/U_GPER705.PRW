#Include "PROTHEUS.CH"
#Include "GPER705.CH"

/*


Ŀ
Funo     GPER705   Autor  Ricardo Berti	                  Data 02/04/2013
Ĵ
Descrio Geracao de TXT sobre CREDITO FISCAL E RETENCOES DOS EMPREGADOS-BOLIVIA
           			Dados extraidos a partir do relatorio RC-IVA - GPER700      
           		Obs.: Demais funcoes encontram-se no fonte GPER700.PRW 			
           		ATENCAO-> Sempre incluir dependencia do fonte GPER700.PRW		
Ĵ
Sintaxe    GPER705()                                                            
Ĵ
Parametros                                                                      
Ĵ
 Uso       Generico                                                             
Ĵ
         ATUALIZACOES SOFRIDAS DESDE A CONSTRUAO INICIAL                        
Ĵ
Programador  Data      CHAMADO        Motivo da Alteracao                     
Ĵ
                                                                              
Jonathan Glz06/05/2015    PCREQ-4256Se elimina la funcion AjustaSx1,la cual   
                                    realiza la modificacion aldiccionario de  
                                    datos(SX1) por motivo de adecuacion  nueva
                                    estructura de SXs para version 12         
ٱ


*/
User Function GPER705()

Local aGetArea	:= GetArea()
Local cPerg		:= "XGPR705"//"GPR705"
Local nOpca		:= 0
Local aSays		:= {}, aButtons := {} //<== arrays locais de preferencia

Private Titulo	:= STR0001 // "Gerao do arquivo TXT - Planilha Tributria"
Private cFilialDe
Private cFilialAte
Private cMesAno
Private cMes
Private cAno
Private cMatDe
Private cMatAte
Private cCustoDe
Private cCustoAte
Private cNomeDe
Private cNomeAte
Private cSit
Private cCat
Private nOrdem
Private cArqNome
Private cProcessos
Private cProcedi

CriaSX1(cPerg)//Creando preguntas para ser utilizadas en la generacion del TXT
Pergunte(cPerg,.F.)

AADD(aSays,STR0002)		//"Este programa gera o arquivo TXT da planilha tributria - informaes sobre"
AADD(aSays,STR0003)		//"Crdito Fiscal e Retenes dos funcionrios para uso no Sistema DAVINCI."

AADD(aButtons, { 5,.T.,{|| Pergunte(cPerg,.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca := 1,IF(gpTudoOK(cPerg),FechaBatch(),nOpca:=0) }} )
AADD(aButtons, { 2,.T.,{|o| FechaBatch() }} )

FormBatch( Titulo, aSays, aButtons )

If nOpca == 1

	Processa({|lEnd| GPR705Proc(),Titulo})		// esta funcao esta' em GPER700
	//Processa({|lEnd| staticCall(U_GPER700, GPR705Proc,,, .T.),Titulo})		// esta funcao esta' em GPER700

EndIf

RestArea(aGetArea)

Return Nil

/*


Ŀ
Funao     gpTudoOK  Autor  Ricardo Berti	                   Data 02/04/2013
Ĵ
Descriao Validacao dos dados antes de executar o processo						
          		                                                				
Ĵ
Sintaxe    gpTudoOK(ExpC1)	                                                    
Ĵ
Parametros ExpC1: Grupo de pergunte 		                                    
Ĵ
 Uso       GPER705                                                              
ٱ

*/
Static Function gpTudoOK(cPerg)

Local lRet	:= .T.
Local cFile	:= AllTrim(mv_par12)

Pergunte(cPerg,.F.)

If Empty(mv_par03)
	Help(" ",1,"XINVMESANO")
	lRet := .F.
EndIf

//Ŀ
//Verifica o Arquivo TXT                                     
//
If lRet .And. File(cFile) .And. !MsgYesNo(STR0004+CRLF+STR0005)	//  "El archivo existe."###"Desea sobrescribir ?"
	lRet := .F.
Endif

Return(lRet)


Static Function CriaSX1(cPerg)  // Crear Preguntas
	/*Esta funcion al ejecutarse crea los registro en la tabla SX1, asi de esta manera podemos utilizar los parametros
	Tomar en cuanta que deben cambiar los parametros segun el caso, se debe indicar cada uno las preguntas con toda las
	especificaciones,
	*/
	xPutSX1(cPerg,"01","Filial De ?" 	, "De Sucursal ?"	,"Branch From ?"		,"MV_CH1","C",04,0,0,"G","","SM0","033","S","MV_PAR01",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"02","Filial Ate ?" 	, "A Sucursal ?"	,"Branch to ?"			,"MV_CH2","C",04,0,0,"G","naovazio()","SM0","033","S","MV_PAR02",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"03","Ms/Ano Competnc.(MMAAAA) ?" , "Mes/Ao Competenc.(MMAAAA) ?"	,"Month/Year Ref.(MMYYYY) ?"	,"MV_CH3","C",11,0,0,"G","FCHKCOMP","","","S","MV_PAR03",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"04","Matricula De ?" , "De matricula ?"	,"Registration From ?"	,"MV_CH4","C",06,0,0,"G","","SRA","","S","MV_PAR04",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"05","Matricula Ate ?" 	, "A matricula ?"	,"Registration To ?"	,"MV_CH5","C",06,0,0,"G","NaoVazio()","SRA","","S","MV_PAR05",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"06","Centro de Custo De ?" , "De centro de costo ?"	,"Cost Center From ?"	,"MV_CH6","C",11,0,0,"G","","CTT","004","S","MV_PAR06",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"07","Centro de Custo At ?" 	, "A centro de costo ?"	,"Cost Center To ?"	,"MV_CH7","C",11,0,0,"G","NaoVazio()","CTT","004","S","MV_PAR07",""       ,""            ,""        ,""     ,""   ,"")	
	xPutSX1(cPerg,"08","Nome De ?" , "De nombre ?"	,"Name From ?"	,"MV_CH8","C",30,0,0,"G","","","","S","MV_PAR08",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"09","Nome Ate ?" , "A nombre ?"	,"Name To ?"	,"MV_CH9","C",30,0,0,"G","NaoVazio()","","","S","MV_PAR09",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"10","Situaes ?" , "Situaciones ?"	,"Status ?"	,"MV_CHA","C",05,0,0,"G","fSituacao()","","","S","MV_PAR10",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"11","Categorias ?" 	, "Categorias ?"	,"Categories ?"	,"MV_CHB","C",15,0,0,"G","fCategoria()","","","S","MV_PAR11",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"12","Path/Nome Arquivo TXT ?" 	, "Path/Nombre archivo TXT ?"	,"Path TXT File Name ?"	,"MV_CHC","C",50,0,0,"G","MV_PAR12:=staticCall(U_GPER705,getDir,,,.T.)","","","S","MV_PAR12",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"13","Processos ?"	, "Procesos ?"	,"Processes ?"	,"MV_CHD","C",90,0,0,"G","fListProc() .and. NaoVazio()","","","S","MV_PAR13",""       ,""            ,""        ,""     ,""   ,"")
	xPutSX1(cPerg,"14","Roteiro ?", "Procedimiento ?"	,"Script ?"	,"MV_CHE","C",30,0,0,"G","fRoteiro() .and. NaoVazio()","","","S","MV_PAR14",""       ,""            ,""        ,""     ,""   ,"")
return

Static Function xPutSx1(cGrupo,cOrdem,cPergunt,cPerSpa,cPerEng,cVar,;
	cTipo ,nTamanho,nDecimal,nPresel,cGSC,cValid,;
	cF3, cGrpSxg,cPyme,;
	cVar01,cDef01,cDefSpa1,cDefEng1,cCnt01,;
	cDef02,cDefSpa2,cDefEng2,;
	cDef03,cDefSpa3,cDefEng3,;
	cDef04,cDefSpa4,cDefEng4,;
	cDef05,cDefSpa5,cDefEng5,;
	aHelpPor,aHelpEng,aHelpSpa,cHelp)

	LOCAL aArea := GetArea()
	Local cKey
	Local lPort := .f.
	Local lSpa := .f.
	Local lIngl := .f.

	cKey := "P." + AllTrim( cGrupo ) + AllTrim( cOrdem ) + "."

	cPyme    := Iif( cPyme           == Nil, " ", cPyme          )
	cF3      := Iif( cF3           == NIl, " ", cF3          )
	cGrpSxg := Iif( cGrpSxg     == Nil, " ", cGrpSxg     )
	cCnt01   := Iif( cCnt01          == Nil, "" , cCnt01      )
	cHelp      := Iif( cHelp          == Nil, "" , cHelp          )

	dbSelectArea( "SX1" )
	dbSetOrder( 1 )

	// Ajusta o tamanho do grupo. Ajuste emergencial para validao dos fontes.
	// RFC - 15/03/2007
	cGrupo := PadR( cGrupo , Len( SX1->X1_GRUPO ) , " " )

	If !( DbSeek( cGrupo + cOrdem ))

		cPergunt:= If(! "?" $ cPergunt .And. ! Empty(cPergunt),Alltrim(cPergunt)+" ?",cPergunt)
		cPerSpa     := If(! "?" $ cPerSpa .And. ! Empty(cPerSpa) ,Alltrim(cPerSpa) +" ?",cPerSpa)
		cPerEng     := If(! "?" $ cPerEng .And. ! Empty(cPerEng) ,Alltrim(cPerEng) +" ?",cPerEng)

		Reclock( "SX1" , .T. )

		Replace X1_GRUPO   With cGrupo
		Replace X1_ORDEM   With cOrdem
		Replace X1_PERGUNT With cPergunt
		Replace X1_PERSPA With cPerSpa
		Replace X1_PERENG With cPerEng
		Replace X1_VARIAVL With cVar
		Replace X1_TIPO    With cTipo
		Replace X1_TAMANHO With nTamanho
		Replace X1_DECIMAL With nDecimal
		Replace X1_PRESEL With nPresel
		Replace X1_GSC     With cGSC
		Replace X1_VALID   With cValid

		Replace X1_VAR01   With cVar01

		Replace X1_F3      With cF3
		Replace X1_GRPSXG With cGrpSxg

		If Fieldpos("X1_PYME") > 0
			If cPyme != Nil
				Replace X1_PYME With cPyme
			Endif
		Endif

		Replace X1_CNT01   With cCnt01
		If cGSC == "C"               // Mult Escolha
			Replace X1_DEF01   With cDef01
			Replace X1_DEFSPA1 With cDefSpa1
			Replace X1_DEFENG1 With cDefEng1

			Replace X1_DEF02   With cDef02
			Replace X1_DEFSPA2 With cDefSpa2
			Replace X1_DEFENG2 With cDefEng2

			Replace X1_DEF03   With cDef03
			Replace X1_DEFSPA3 With cDefSpa3
			Replace X1_DEFENG3 With cDefEng3

			Replace X1_DEF04   With cDef04
			Replace X1_DEFSPA4 With cDefSpa4
			Replace X1_DEFENG4 With cDefEng4

			Replace X1_DEF05   With cDef05
			Replace X1_DEFSPA5 With cDefSpa5
			Replace X1_DEFENG5 With cDefEng5
		Endif

		Replace X1_HELP With cHelp

		PutSX1Help(cKey,aHelpPor,aHelpEng,aHelpSpa)

		MsUnlock()
	Else

		lPort := ! "?" $ X1_PERGUNT .And. ! Empty(SX1->X1_PERGUNT)
		lSpa := ! "?" $ X1_PERSPA .And. ! Empty(SX1->X1_PERSPA)
		lIngl := ! "?" $ X1_PERENG .And. ! Empty(SX1->X1_PERENG)

		If lPort .Or. lSpa .Or. lIngl
			RecLock("SX1",.F.)
			If lPort
				SX1->X1_PERGUNT:= Alltrim(SX1->X1_PERGUNT)+" ?"
			EndIf
			If lSpa
				SX1->X1_PERSPA := Alltrim(SX1->X1_PERSPA) +" ?"
			EndIf
			If lIngl
				SX1->X1_PERENG := Alltrim(SX1->X1_PERENG) +" ?"
			EndIf
			SX1->(MsUnLock())
		EndIf
	Endif

	RestArea( aArea )

Return

static function getDir()
Local cRet:= ""
	cRet:= cGetFile('*.*','',,,,176) + "rciva" + DTOS(DATE()) + ".txt"
return cRet
