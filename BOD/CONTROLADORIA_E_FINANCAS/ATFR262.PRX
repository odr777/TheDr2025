#INCLUDE "ATFR262.CH"
#Include "Protheus.ch"

Static __lDefTop	:= IfDefTopCTB()

/*


Ŀ
Funo     ATFR262     Autor  Felipe C. Cunha        Data  01.08.11 
Ĵ
Descrio  Guia de Abate do Ativo Fixo                                  
Ĵ
Uso        ATFR262                                                      
ٱ


*/
Function ATFR262()
Local oReport   := Nil
Local lOk		:= .T.
Local lImpR4	:= TRepInUse()
Local cPerg		:= 'ATR262'

Private titulo		:= ""
Private nomeprog	:= STR0005 //ATFR262

//Ŀ
// Somento disponivel verso R4                         
//
If !lImpR4
	Help("  ",1,STR0013,,STR0014,1,0)//"Funo disponvel apenas em TReport"
	Return
ENdIf

If !__lDefTop
	MsgAlert(STR0001) //Relatorio disponivel somente para ambientes TOPCONN/DBACESS
	lOk := .T.
Endif

If lOk .And. cPaisLoc <> 'PTG'
	MsgAlert(STR0002)//Relatorio no disponivel para esta localizao
	lOk := .F.
Endif

If lOk .And. lImpR4
	//Ŀ
	// Atualizacao das perguntas utilizadas pelo relatorio  
	//
	
	oReport := ReportDef()
	
	If Valtype( oReport ) == 'O'
		If ! Empty( oReport:uParam )
			Pergunte( oReport:uParam, .F. )
		EndIf
		
		oReport:PrintDialog()
	Endif
Endif
	
oReport := Nil

Return

/*


Ŀ
Funo     ATFR263     Autor  Felipe C. Cunha      Data  01.08.11 
Ĵ
Desc.      Definicao do objeto do relatorio personalizavel e das      
           secoes que serao utilizadas                                
Ĵ
Uso        ATFR262                                                    
ٱ


*/
Static Function ReportDef()

Local oReport,oSection1,oSection2,oSection3
Local cReport := "ATFR262"
Local cAlias1 := "SN4"
Local cAlias2 := "SN1"
Local cPerg	  := "ATR262"
Local cTitulo := OemToAnsi(STR0003) // Guia de Abate
Local cDescri := OemToAnsi(STR0004) // "Este programa ir emitir a relao dos Bens que foram baixador por Extravio/Obsolncia"


oReport	:= TReport():New( cReport,Capital(cTitulo),cPerg, { |oReport| Pergunte(cPerg , .F. ), If(! ReportPrint( oReport ), oReport:CancelPrint(), .T. ) }, cDescri ) 

//Ŀ
// Define a 1a. secao do relatorio - SUBTOTAIS          
//
oSection1 := TRSection():New( oReport, STR0006, {cAlias1,cAlias2}) /*"Numero Documento"*/
TRCell():New( oSection1, "DOC"  , /*cAlias1*/ ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Documento
TRCell():New( oSection1, "DOC1" , /*cAlias1*/ ,/*X3Titulo*/,/*Picture*/,/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/)	// Numero Documento
oSection1:setHeaderSection(.F.) //Inibe Cabealho

//Ŀ
// Define a 2a. secao do relatorio - VALORES            
//
oSection2 := TRSection():New( oReport, cTitulo, {cAlias1,cAlias2} )
TRCell():New( oSection2, "N4_CBASE"  , cAlias1    ,STR0007,/*Picture*/           ,15/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"LEFT" ,,"LEFT")	// Codigo
TRCell():New( oSection2, "N1_DESCRIC", cAlias2    ,STR0008,/*Picture*/           ,70           ,/*lPixel*/,/*{|| code-block de impressao }*/,"LEFT" ,,"LEFT" )// Descricao
TRCell():New( oSection2, "N4_QUANTD" , cAlias1    ,STR0009,X3Picture("N4_QUANTD"),18           ,/*lPixel*/,/*{|| code-block de impressao }*/,"LEFT" ,,"LEFT" )// Quantidade
TRCell():New( oSection2, "N1_BAIXA"  , cAlias2    ,STR0015,/*Picture*/           ,12           ,/*lPixel*/,/*{|| code-block de impressao }*/,"LEFT" ,,"LEFT" )// Dt.Abate
TRCell():New( oSection2, "VALOR1"    , cAlias1    ,STR0010,X3Picture("N4_VLROC1"),17           ,/*lPixel*/,/*{|| code-block de impressao }*/,"RIGHT",,"RIGHT")// Valor Moeda 01
TRCell():New( oSection2, "SUBTOTAL1" , /*cAlias1*/,STR0011,X3Picture("N4_VLROC1"),17           ,/*lPixel*/,/*{|| code-block de impressao }*/,"RIGHT",,"RIGHT")// SubTotal
oReport:SetTotalInLine(.F.)

//Ŀ
// Define a 3a. secao do relatorio - SUBTOTAIS          
//
oSection3 := TRSection():New( oReport,STR0012, {cAlias1,cAlias2})//"Total Geral"
TRCell():New( oSection3, "DESCR", /*cAlias1*/," ",/*Picture*/           ,182/*Tamanho*/,/*lPixel*/,/*{|| code-block de impressao }*/,"RIGHT",,"RIGHT")	// Valor Moeda 01
TRCell():New( oSection3, "TOTAL", /*cAlias1*/," ",X3Picture("N4_VLROC1"),17            ,/*lPixel*/,/*{|| code-block de impressao }*/,"RIGHT",,"RIGHT")// Valor Moeda 01
//oSection3:setHeaderSection(.F.) //Inibe Cabealho

Return oReport      


/*                                                        


Ŀ
Funo     ATFR262     Autor  Felipe C. Cunha      Data  01.08.11 
Ĵ
Desc.     Query de impressao do relatorio                             
͹
Uso        ATFR262                                                    
ͼ


*/
Static Function ReportPrint( oReport )
Local oSection1 := oReport:Section(1)
Local oSection2 := oReport:Section(2)
Local oSection3 := oReport:Section(3)

Local cQuery  := "SN4"
Local cWhere  := ""

PRIVATE nSubTot := 0  
PRIVATE NomeProg := ''

titulo	:= oReport:Title()

oReport:SetCustomText( {|| Cabec262( oReport ) })  

//Ŀ
// Valida o motivo de baixa a ser exibido               
//
IF mv_par08 = 1
	cWhere := "% N4_MOTIVO = '02' AND %"	
ELSEIF mv_par08 = 2
	cWhere := "% N4_MOTIVO = '06' AND %"	
ELSEIF mv_par08 = 3
	cWhere := "% (N4_MOTIVO = '02' OR N4_MOTIVO = '06') AND %"	
ENDIF


#IFDEF TOP
	cQuery := GetNextAlias()	
	oSection2:BeginQuery() 

	BeginSql Alias cQuery
		SELECT
			N4_CBASE,
			N4_ITEM,
			N1_DESCRIC,
		    N4_QUANTD,	
		    N4_VLROC1,
		    N1_BAIXA,
		    (SELECT SUM(N4_VLROC1) 
				FROM %TABLE:SN4% SN4 
    			WHERE  	%EXP:CWHERE% //VALIDA MOTIVO DE BAIXA	
		    	(N4_TIPOCNT = '4') 
    			AND N4_CBASE = (N1_CBASE)
    			AND SN4.%NOTDEL%) AS VALOR     			

		FROM 
			%TABLE:SN4% SN4
   			LEFT JOIN %TABLE:SN1% SN1  ON SN1.N1_CBASE  = SN4.N4_CBASE
      								  AND SN1.N1_ITEM   = SN4.N4_ITEM
   			                          AND SN1.N1_FILIAL = SN4.N4_FILIAL							   			                          
		WHERE
			SN4.N4_CBASE BETWEEN %EXP:MV_PAR02%       AND %EXP:MV_PAR03%       AND						
			SN4.N4_ITEM  BETWEEN %EXP:MV_PAR04%       AND %EXP:MV_PAR05%       AND						
			SN4.N4_DATA  BETWEEN %EXP:DTOS(MV_PAR06)% AND %EXP:DTOS(MV_PAR07)% AND    		
			SN4.N4_FILIAL  = %XFILIAL:SN4%  AND  
			N4_TIPOCNT = '1' AND
			N4_OCORR = '01' AND
			%EXP:CWHERE% //VALIDA MOTIVO DE BAIXA											
			SN4.%NOTDEL%       AND   
			SN1.%NOTDEL%	 
	GROUP BY N4_CBASE,N1_CBASE, N4_ITEM, N1_DESCRIC, N4_QUANTD, N4_VLROC1,N4_MOTIVO	, N1_BAIXA
	ORDER BY N4_CBASE, N4_ITEM	
  	EndSql
	oSection2:EndQuery()	
#ENDIF
                             

oSection2:Cell("N4_CBASE") :SetBlock({|| ALLTRIM(N4_CBASE) + "-" + ALLTRIM(N4_ITEM)  })
oSection2:Cell("VALOR1")   :SetBlock({|| ( cQuery )->N4_VLROC1 - ( cQuery )->VALOR   })
oSection2:Cell("SUBTOTAL1"):SetBlock({|| ( cQuery )->N4_VLROC1 - ( cQuery )->VALOR   })
oSection2:init()
TcSetField(cQuery,"VALOR","N",TamSX3("N4_VLROC1")[1],TamSX3("N4_VLROC1")[2]) 
While !Eof() 
	nSubTot += ( cQuery )->N4_VLROC1 - ( cQuery )->VALOR
	oSection2:Printline()
	DbSkip()
EndDo	
oSection2:Finish()


oSection3:Cell("DESCR"):SetBlock({|| "Total" })
oSection3:Cell("TOTAL"):SetBlock({|| nSubTot })
oSection3:init()
oSection3:Printline()
oSection3:Finish()

Return .T.


/*


Ŀ
Funo     Cabec262    Autor  Felipe C. Cunha      Data  01.08.11 
Ĵ
Desc.     Insere novas perguntas ao SX1                               
͹
Uso        ATFR262                                                    
ͼ


*/
Static Function Cabec262( oReport )
Local aArea		:= GetArea()
Local aCabec	:= {}
Local cChar		:= chr(160)  // caracter dummy para alinhamento do cabealho

SX3->( DbSetOrder(2) )
SX3->( MsSeek( "A1_CGC" , .t.))

If SM0->(Eof())                                
	SM0->( MsSeek( cEmpAnt + cFilAnt , .T. ))
Endif

aCabec := {	"__LOGOEMP__" , cChar + "         " + SM0->M0_NOMECOM ;
	      + "         " + cChar + RptFolha+ TRANSFORM(oReport:Page(),'999999');
          , cChar + "         " + Transform( Alltrim( SM0->M0_CGC ), alltrim( SX3->X3_PICTURE ));
          + "         " + cChar + " Numero Doc.: " + MV_PAR01 ;
          , "SIGA / " + STR0005 + " /v." + cVersao ; //"SIGA / ATFR262 /v."
          + "         " + cChar + UPPER(AllTrim(titulo)) ;
          + "         " + cChar;
          , RptHora + " " + time() ;
          + "         " + cChar + RptEmiss + " " + Dtoc(dDataBase) }
          
RestArea( aArea )

Return aCabec