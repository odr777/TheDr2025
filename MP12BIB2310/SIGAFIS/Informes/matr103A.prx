#INCLUDE "MATR103A.ch"
#INCLUDE "PROTHEUS.CH" 

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR103A ³ Autor ³ Julio C.Guerato		³ Data ³ 13/03/12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Registro Auxiliar - Bolivia		                          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Compras 								                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function MATR103A
Local oReport
Private cPerg		:= 'MTR103A'
Private cAliasReg 	:= 'REGAUX'

oReport := ReportDef()
oReport:PrintDialog()

Return

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportDef ³ Autor ³TOTVS				    ³ Data ³13.03.12  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³A funcao estatica ReportDef devera ser criada para todos os ³±±
±±³          ³relatorios que poderao ser agendados pelo usuario.          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³ExpO1: Objeto do relatório                                  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportDef()
Local oReport 
Local oSection1

AjustaSX1()

Pergunte(cPerg,.F.)

oReport:= TReport():New("MATR103A",STR0001,"MTR103A", {|oReport| ReportPrint(oReport)},STR0001)   

oReport:SetLandscape()    
oSection1:=TRSection():New(oReport,STR0001,{})
oSection1 :SetTotalInLine(.F.)
oSection1 :SetReadOnly()  
oSection1 :SetHeaderPage(.F.)

//Campos para Impressao //
If MV_PAR01 == 1     
	TRCell():New(oSection1,"MODALID" ,"",STR0005,/*Picture*/					,1							  ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"DATADOC" ,"",STR0006,PesqPict("SF1","F1_EMISSAO"),TAMSX3("F1_EMISSAO")[1]   ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"TIPOTRAN","",STR0007,/*Picture*/					,5							  ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"NIT"	 ,"",STR0008,PesqPict("SA6","A6_CGC")		,20							  ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"NOME"	 ,"",STR0009,/*Picture*/						,TAMSX3("A2_NOME")[1]	  ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"NRDOC"	 ,"",STR0010,/*Picture*/						,TAMSX3("F1_DOC")[1]+4	  ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"VALOR"	 ,"",STR0011,PesqPict("SF1","F1_VALBRUT") ,TAMSX3("F1_VALBRUT")[1]+4,/*lPixel*/,,"RIGHT",,"RIGHT")
	TRCell():New(oSection1,"NUMAUT"	 ,"",STR0012,/*Picture*/						,TAMSX3("F1_NUMAUT")[1]	  ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"CTPAGO"	 ,"",STR0013,/*Picture*/						,TamSX3("EK_CONTA")[1]	  ,/*lPixel*/,{|| aRetFin[1]})
	TRCell():New(oSection1,"VALPAGO" ,"",STR0014,PesqPict("SEK","EK_VALOR")	,TamSX3("EK_VALOR")[1]	  ,/*lPixel*/,{|| aRetFin[2]})
	TRCell():New(oSection1,"VALACUM" ,"",STR0015,PesqPict("SEK","EK_VALOR")	,TamSX3("EK_VALOR")[1]	  ,/*lPixel*/,{|| aRetFin[3]})
	TRCell():New(oSection1,"NITFIN"  ,"",STR0016,PesqPict("SA6","A6_CGC")		,TamSX3("A6_CGC")[1]		  ,/*lPixel*/,{|| aRetFin[4]})
	TRCell():New(oSection1,"DOCPAGO" ,"",STR0017,/*Picture*/					,TamSX3("EK_NUM")[1]		  ,/*lPixel*/,{|| aRetFin[5]})
	TRCell():New(oSection1,"TPPAGO"  ,"",STR0018,/*Picture*/					,2							  ,/*lPixel*/,{|| IIf(aRetFin[6] $ "CA|CH","1",IIf(aRetFin[6] == "TF","4","10")) })
	TRCell():New(oSection1,"DTPAGO"  ,"",STR0027,PesqPict("SEK","EK_EMISSAO"),16							  ,/*lPixel*/,{|| StoD(aRetFin[7]) })	
Else
	TRCell():New(oSection1,"MODALID" ,"",STR0005,/*Picture*/				   ,1							,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"DATADOC" ,"",STR0006,PesqPict("SF2","F2_EMISSAO"),TAMSX3("F2_EMISSAO")[1] ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"TIPOTRAN","",STR0007,/*Picture*/					,5							,/*lPixel*/,/*{|| code-block de impressao }*/) 
	TRCell():New(oSection1,"NRDOC"	 ,"",STR0010,/*Picture*/						,TAMSX3("F2_DOC")[1]+10	,/*lPixel*/,/*{|| code-block de impressao }*/)
   TRCell():New(oSection1,"VALOR"	 ,"",STR0011,PesqPict("SF2","F2_VALBRUT") ,TAMSX3("F2_VALBRUT")[1],/*lPixel*/,,"RIGHT",,"RIGHT")
	TRCell():New(oSection1,"NUMAUT" ,"",STR0012,/*Picture*/						,TAMSX3("F2_NUMAUT")[1] ,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"NIT"	 ,"",STR0008,PesqPict("SA6","A6_CGC")		,20							,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"NOME"	 ,"",STR0009,/*Picture*/						,TAMSX3("A1_NOME")[1]	,/*lPixel*/,/*{|| code-block de impressao }*/)
	TRCell():New(oSection1,"CTPAGO"	 ,"",STR0013,/*Picture*/						,TamSX3("EL_CONTA")[1]	,/*lPixel*/,{|| aRetFin[1] })
	TRCell():New(oSection1,"VALPAGO" ,"",STR0014,PesqPict("SEL","EL_VALOR")  ,TamSX3("EL_VALOR")[1]	,/*lPixel*/,{|| aRetFin[2] })
	TRCell():New(oSection1,"VALACUM" ,"",STR0015,PesqPict("SEL","EL_VALOR")  ,TamSX3("EL_VALOR")[1]	,/*lPixel*/,{|| aRetFin[3] })
	TRCell():New(oSection1,"NITFIN"  ,"",STR0016,PesqPict("SA6","A6_CGC")    ,TamSX3("A6_CGC")[1]		,/*lPixel*/,{|| aRetFin[4] })
	TRCell():New(oSection1,"DOCPAGO" ,"",STR0017,/*Picture*/					,TamSX3("EL_NUMERO")[1]+4,/*lPixel*/,{|| aRetFin[5] })
	TRCell():New(oSection1,"TPPAGO"  ,"",STR0018,/*Picture*/					,2							,/*lPixel*/,{|| IIf(aRetFin[6] == "CH","1",IIF(aRetFin[6] == "TF","8","10")) })
	TRCell():New(oSection1,"DTPAGO"  ,"",STR0027,PesqPict("SEL","EL_EMISSAO"),16							,/*lPixel*/,{|| StoD(aRetFin[7]) })	
EndIF	

Return(oReport)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ReportPrin³ Autor ³TOTVS 					³ Data ³13.03.2012³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Impressão do Relatório									  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ExpO1: Objeto Report do Relatório                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function ReportPrint(oReport)

Local oSection1 	:= oReport:Section(1) 
Local nOrdem		:= oReport:Section(1):GetOrder()  

Local cSelect   := "" 
Local cWhere1   := ""
Local cOrder    := ""
Local cTit      := ""
Local cChaveFin := ""
Local cFile     := ""
Local cDelim    := "|"
Local cText     := ""
Local nMod      := 0
Local nValor    := 0 
Local nHand     := 0
Local nTipo     := 0    
Local nX		:= 0
Local lGeraLF	:= .F.
Local cItem		:= ""
Local cChaveD1	:= ""

// -- Arquivo Texto -- //
cFile:=IIF(MV_PAR01==1,STR0002,STR0003)+"_"+STR0023+"_"+StrZero(Month(MV_PAR03),2)+StrZero(Year(MV_PAR03),4)+"_"+Trim(SM0->M0_CGC)+".TXT"
nHand:=FCreate(cFile)

// -- Seta Titulo do Relatório -- //    
oReport:SetCustomText( {|| Cabec103A(oReport ) })  

// -- Define o Tipo de Relatorio para busca dos Dados -- //
If MV_PAR01 == 1   //Compras

	If MV_PAR02 ==1
		cSelect := "% F1_DTDIGIT DATADOC, %"
	Else
		cSelect:="% F1_EMISSAO DATADOC, %"
	EndIf

	If MV_PAR05 == 2
			cWhere1 := " % AND F1_MOEDA = 1  AND F1_VALBRUT >=50000%"
	EndIf
	
	cOrder := "% "+IIF(MV_PAR02==1,"F1_DTDIGIT","F1_EMISSAO")+", F1_DOC %"
	
	BeginSql Alias cAliasReg   
	
	Column DataDoc as Date
	
	Select F1_FILIAL FILIAL, F1_FORNECE CLIFOR, F1_LOJA LOJA,       F1_COND COND,   F1_MOEDA MOEDA,     F1_TXMOEDA TXMOEDA, %Exp:cSelect%
	       '1' TIPOTRAN,     A2_CGC NIT,        A2_NOME NOME,       F1_DOC NRDOC,   F1_PREFIXO PREFIXO, F1_DUPL DUPLIC,     F1_EMISSAO EMISSAO, 
	       F1_VALBRUT VALOR, F1_NUMAUT NUMAUT,  F1_ESPECIE ESPECIE, F1_SERIE SERIE, A2_EST ESTADO   
  	  From %table:SF1% SF1 , %table:SA2% SA2
  	 		Where SF1.F1_FILIAL   = %xFilial:SF1%
	    		And SA2.A2_FILIAL  = %xFilial:SA2%   
   	    		And SF1.F1_COND   Not In("  ")
       		And SA2.A2_COD     = SF1.F1_FORNECE
      			And SA2.A2_LOJA    = SF1.F1_LOJA
       		And SF1.%NotDel%
       		And SA2.%NotDel%
 	 ORDER BY %Exp:cOrder%
 	 
	EndSql 
 
Else			    //Vendas  
		
	If MV_PAR02 ==1
		cSelect := "% F2_DTDIGIT DATADOC, %"
	Else
		cSelect:="% F2_EMISSAO DATADOC, %"
	EndIf

	If MV_PAR05 == 2
			cWhere1 := " % AND F2_MOEDA = 1  AND F2_VALBRUT >=50000 %"
	EndIf
	
	cOrder := "% "+IIF(MV_PAR02==1,"F2_DTDIGIT","F2_EMISSAO")+", F2_DOC %"
	
	BeginSql Alias cAliasReg   
	
	Column DataDoc as Date
	
	Select F2_FILIAL FILIAL, F2_CLIENTE CLIFOR, F2_LOJA LOJA,       F2_COND COND,   F2_MOEDA MOEDA,     F2_TXMOEDA TXMOEDA, %Exp:cSelect%
	       '1' TIPOTRAN,     A1_CGC NIT,        A1_NOME NOME,       F2_DOC NRDOC,   F2_PREFIXO PREFIXO, F2_DUPL DUPLIC,     F2_EMISSAO EMISSAO, 
	       F2_VALBRUT VALOR, F2_NUMAUT NUMAUT,  F2_ESPECIE ESPECIE, F2_SERIE SERIE, A1_EST ESTADO   
  	  From %table:SF2% SF2 , %table:SA1% SA1
	  	 Where SF2.F2_FILIAL  = %xFilial:SF2%
		    And SA1.A1_FILIAL  = %xFilial:SA1%   
	   	    And SF2.F2_COND   Not In("  ")
	       And SA1.A1_COD     = SF2.F2_CLIENTE
	       And SA1.A1_LOJA    = SF2.F2_LOJA
	       And SF2.%NotDel%
	       And SA1.%NotDel%
 	 ORDER BY %Exp:cOrder%
 	 
	EndSql 
 
EndIf    
oReport:Section(1):EndQuery()  

//Inicia Impressão dos documentos obtidos //
oSection1:Init()
While !oReport:Cancel() .And. (cAliasReg)->(!Eof())
	If oReport:Cancel()
		Exit
	EndIf     

	If MV_PAR01 == 1 
		// Exibe apenas Facturas ENTRADAS com TES que gera Livro Fiscal
		lGeraLF := .F.
		cItem	:= xFilial("SD1")+(cAliasReg)->(NRDOC+SERIE+CLIFOR+LOJA)
		cChaveD1:= "SD1->D1_FILIAL+SD1->D1_DOC+SD1->D1_SERIE+SD1->D1_FORNECE+SD1->D1_LOJA"
		DbSelectArea("SD1")
		DbSetOrder(1)
		MsSeek(cItem)
		While (&cChaveD1)==cItem .And. !Eof()
			DbSelectArea("SF4")
			DbSetOrder(1)
			MsSeek(xFilial("SF4")+SD1->D1_TES)
			If SF4->F4_GERALF = "1"
				lGeraLF := .T.
				Exit
			EndIf
			DbSelectArea("SD1")
			DbSkip()
		End
		If !lGeraLF
			dbSelectArea(cAliasReg)
			dbSkip()
			Loop
		EndIf
		
	Else
			// Exibe apenas Facturas SAIDAS com TES que gera Livro Fiscal
		lGeraLF := .F.
		cItem	:= xFilial("SD2")+(cAliasReg)->(NRDOC+SERIE+CLIFOR+LOJA)
		cChaveD1:= "SD2->D2_FILIAL+SD2->D2_DOC+SD2->D2_SERIE+SD2->D2_CLIENTE+SD2->D2_LOJA"
		DbSelectArea("SD2")
		DbSetOrder(3)
		MsSeek(cItem)
		While (&cChaveD1)==cItem .And. !Eof()
			DbSelectArea("SF4")
			DbSetOrder(1)
			MsSeek(xFilial("SF4")+SD2->D2_TES)
			If SF4->F4_GERALF = "1"
				lGeraLF := .T.
				Exit
			EndIf
			DbSelectArea("SD2")
			DbSkip()
		End
		If !lGeraLF
			dbSelectArea(cAliasReg)
			dbSkip()
			Loop
		EndIf
		
	EndIf
    nMod :=0
    nTipo:= 0
	
	//Identifica se a Factura e a 1=A vista / 2=A prazo //  
	If MV_PAR01 == 1                                              
	   cTit:=xFilial("SE2")+(cAliasReg)->(CLIFOR+LOJA+PREFIXO+DUPLIC)
	   cChaveFin:="E2_FILIAL+E2_FORNECE+E2_LOJA+E2_PREFIXO+E2_NUM"
	   DbSelectArea("SE2")
	   DbSetOrder(6)
	   MsSeek(cTit)
	   Do While (&cChaveFin)==cTit .And. !Eof()
	   		nMod++
	   	  	DbSkip()	   
	   EndDo
	   nMod:=IIF(nMod>1 .Or. nMod==0,2,nMod)
	Else //faturamento
	   cTit:=xFilial("SE1")+(cAliasReg)->(CLIFOR+LOJA+PREFIXO+DUPLIC)
	   cChaveFin:="E1_FILIAL+E1_CLIENTE+E1_LOJA+E1_PREFIXO+E1_NUM"
	   DbSelectArea("SE1")
	   DbSetOrder(2)
	   MsSeek(cTit)
	   Do While (&cChaveFin)==cTit .And. !Eof()
			nMod++	   
	   	  	DbSkip()	   
	   EndDo
	   nMod:=IIF(nMod>1 .Or. nMod==0,2,nMod)	   
	EndIf    
	
	//Quando Titulo estiver em outra Moeda, faz a conversao para Bolivianos //
	If MV_PAR05 == 1 
		If (cAliasReg)->MOEDA<>1  
		   nValor:=xMoeda((cAliasReg)->VALOR,(cAliasReg)->MOEDA, 1, (cAliasReg)->DATADOC)
		Else
			nValor:=(cAliasReg)->VALOR
		EndIf
		
		//Valor inferior a 50.000(Bolivianos), ignora
		If nValor<50000  
			dbSelectArea(cAliasReg)
			dbSkip()
			Loop
		EndIf
	Else
		nValor:=(cAliasReg)->VALOR
	EndIf

	aRetFin := {"",0,0,"","",""}

	aRetTotFin := aClone(M103RTFin())

	// Tipo Transacao //
	nTipo:=TipoTransacao()

    // Seta valores / imprime //
	oSection1:Cell("MODALID"):SetValue(nMod)
	oSection1:Cell("TIPOTRAN"):SetValue(nTipo)
	oSection1:Cell("VALOR"):SetValue(nValor)

	For nX := 1 To Len(aRetTotFin)
		aRetFin := aClone(aRetTotFin[nX])	
		oReport:IncMeter() 
		oSection1:PrintLine()
		
		// Grava arquivo Texto //
		cText:=STR(nMod,1)+cDelim+Dtoc(MV_PAR03)+cDelim+(cAliasReg)->TIPOTRAN+cDelim+(cAliasReg)->NIT+cDelim+;
		       (cAliasReg)->NOME+cDelim+(cAliasReg)->NRDOC+cDelim+Str(nValor)+cDelim+(cAliasReg)->NUMAUT+cDelim+;
		       aRetFin[1]+cDelim+Str(aRetFin[2])+cDelim+Str(aRetFin[3])+cDelim+aRetFin[4]+cDelim+aRetFin[5]+cDelim+;
		       IIf(MV_PAR01 == 1,IIf(aRetFin[6] $ "CA|CH","1",IIf(aRetFin[6] == "TF","4","10")),IIf(aRetFin[6] == "CH","1",IIF(aRetFin[6] == "TF","8","10")))+;
		       cDelim+aRetFin[7]+cDelim+Chr(10)
		       
		FWrite(nHand,cText)
	Next nX	

	dbSelectArea(cAliasReg)
	dbSkip()
EndDo   
oSection1:Finish()  

//Fecha arquivo texto //
Fclose(nHand)
                 
//Responsavel pelas informacoes //
oReport:SkipLine(3)                       
oReport:Say(oReport:Row(), 10,  MV_PAR06)  
oReport:Say(oReport:Row(), 700, MV_PAR07) 
oReport:SkipLine(1)
oReport:Say(oReport:Row(), 10, Replicate("-",Len(STR0019)))
oReport:Say(oReport:Row(), 700, Replicate("-",50))
oReport:SkipLine(1)
oReport:Say(oReport:Row(), 10, STR0019)
oReport:Say(oReport:Row(), 700, STR0020)

Return NIL   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ TipoTransacao³ Autor ³ TOTVS		        ³ Data ³ 27.04.12 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ºDesc.     ³ Identifica o Tipo de Transação	                          º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATR103A                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function TipoTransacao()
Local nTipo :=0
Local cSFE
Local aArea :=GetArea()

If MV_PAR01 == 1       
    //Verifica se o titulo possui retenção se possuir, o tipo de transação será igual a 4
    //Caso contrário, valerá as demais regras
   	DbSelectArea("SFE")
   	DbSetOrder(4)
   	cSFE:=xFilial("SFE")+(cAliasReg)->CLIFOR+(cAliasReg)->LOJA+(cAliasReg)->NRDOC+(cAliasReg)->SERIE
   	MsSeek(cSFE)
   	If !EOF()
   		nTipo:=4
   	Else
   		IF Trim((cAliasReg)->ESPECIE)=="NF" .And. Trim((cAliasReg)->ESTADO)<>"EX"
			nTipo:=1
	    ElseIF Trim((cAliasReg)->ESPECIE)=="NF" .And. Trim((cAliasReg)->ESTADO)=="EX"
    		nTipo:=3   
    	Else
			nTipo:=5
    	EndIf
    EndIf
Else   
	IF Trim((cAliasReg)->ESPECIE)=="NF" .And. Trim((cAliasReg)->ESTADO)<>"EX"
		nTipo:=1
    ElseIF Trim((cAliasReg)->ESPECIE)=="NF" .And. Trim((cAliasReg)->ESTADO)=="EX"
   		nTipo:=2   
   	Else
		nTipo:=3
   	EndIf
	      
EndIf

RestArea(aArea)

Return nTipo

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Cabec262   ³ Autor ³ TOTVS		        ³ Data ³ 14.03.11 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±ºDesc.     ³Cabeçalho						                              º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ MATR103A                                                   º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function Cabec103A( oReport )
Local aArea		:= GetArea()
Local aCabec	:= {}
Local cChar		:= chr(160)  // caracter dummy para alinhamento do cabeçalho     
Local cTitulo   := ""


cTitulo:= STR0001+" - "+IIF(MV_PAR01==1,STR0002,STR0003)+" "+STR0004+" - "+STR0021+" "+DTOC(MV_PAR03)+" - "+DTOC(MV_PAR04)

//Obtem Mascara para NIT //
SX3->( DbSetOrder(2) )                   
SX3->( MsSeek( "A1_CGC" , .t.))

aCabec := {	"__LOGOEMP__" , cChar + "         " + SM0->M0_NOMECOM ;
	      + "         " + cChar + RptFolha+ TRANSFORM(oReport:Page(),'999999');
          , cChar + "    "+STR0008+" " + Transform( Alltrim( SM0->M0_CGC ), alltrim( SX3->X3_PICTURE ));
          , "SIGA /" + 'MATR103A' + " /v." + cVersao ; 
          + "         " + cChar + UPPER(AllTrim(cTitulo)) ;
          + "         " + cChar;
          , RptHora + " " + time() ;
          + "         " + cChar + RptEmiss + " " + Dtoc(dDataBase),;
          + STR0024+" "+Trim(SM0->M0_NOME)+" / " + STR0025+" "+Trim(SM0->M0_FILIAL)+" / "+STR0026+" "+Trim(SM0->M0_ENDCOB)}

RestArea( aArea )
Return aCabec

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ AjustaSX1 ³ Autor ³ TOTVS			    ³ Data ³13.03.12  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Altera descricao da pergunta no SX1                        ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()

Local aArea     := GetArea()
Local aHelpPor	:= {}
Local aHelpEng	:= {}
Local aHelpSpa	:= {}
Local aHelpPor2	:= {}
Local aHelpEng2	:= {}
Local aHelpSpa2	:= {} 

/* -- Pergunta: 01 -- */
aHelpPor := {"Indica o tipo de livro a ser impresso."}
aHelpSpa := {"Indica el tipo de libro que se imprimira."}
aHelpEng := {"Indicates book type to be printed."} 
PutSx1(cPerg,"01","Livro ?","Libro","","MV_CH1","N",1,0,1,"C","","","","S","MV_PAR01","Compras","Compra","","","Vendas","Venta","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
PutSX1Help("P"+".MTR103A01.",aHelpPor,aHelpEng,aHelpSpa,.T.)	

/* -- Pergunta: 02 -- */
aHelpPor := {"Indica qual a data que será utilizada no"," processamento do relatório."}
aHelpSpa := {"Indica la fecha que se utilizara en el"," procesamiento del informe."}
aHelpEng := {"Indicates date to be used in report processing."}
PutSx1(cPerg,"02","Ordem ?","Ordenación","","MV_CH2","N",1,0,1,"C","","","","S","MV_PAR02","Digitação","Digitación","","","Emissão","Emisión","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
PutSX1Help("P"+".MTR103A02.",aHelpPor,aHelpEng,aHelpSpa,.T.)	

/* -- Pergunta: 03 -- */
aHelpPor := {"Informar a Data Inicial."}
aHelpSpa := {"Informar la fecha inicial."}
aHelpEng := {"Enter Start Date."} 
PutSx1(cPerg,"03","Data Inicial","Fecha Inicial","","MV_CH3","D",TAMSX3("F1_EMISSAO")[1],0 ,0 ,"G","","","","S","MV_PAR03","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
PutSX1Help("P"+".MTR103A03.",aHelpPor,aHelpEng,aHelpSpa,.T.)	


/* -- Pergunta: 04 -- */
aHelpPor := {"Informar a Data Final."}
aHelpSpa := {"Informar la fecha final."}
aHelpEng := {"Enter End Date."} 
PutSx1(cPerg,"04","Data Final","Fecha Final","","MV_CH4","D",TAMSX3("F1_EMISSAO")[1],0 ,0 ,"G","","","","S","MV_PAR04","","","","","","","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
PutSX1Help("P"+".MTR103A04.",aHelpPor,aHelpEng,aHelpSpa,.T.)	    

/* -- Pergunta: 05 -- */
aHelpPor := {"Considera titulos em outras moedas."}                      
aHelpSpa := {"Considera titulos en otras monedas."}
aHelpEng := {"Considers bills in other currencies."}
PutSx1(cPerg,"05","Titulos em outras Moedas ?","Títulos en otras monedas ?","","MV_CH5","N",1,0,1,"C","","","","S","MV_PAR05","Converter","Convertir","","","Ignorar","Ignorar","","","","","","","","","","",aHelpPor,aHelpEng,aHelpSpa)
PutSX1Help("P"+".MTR103A05.",aHelpPor,aHelpEng,aHelpSpa,.T.)	

/* -- Pergunta: 06 -- */
aHelpPor := {"CI do Responsável pelo Documento."}                      
aHelpSpa := {"CI del responsable por el documento."}
aHelpEng := {"CI of Responsible for the Document."}
PutSx1(cPerg,"06","CI do responsável","CI del responsable" ,"" ,"MV_CH6","C",20, 0 ,0 ,"G","","","","S","MV_PAR06","","","","","","","","","","","","","","","","",aHelpPor ,aHelpEng ,aHelpSpa)
PutSX1Help("P"+".MTR103A06.",aHelpPor,aHelpEng,aHelpSpa,.T.)

/* -- Pergunta: 07 -- */
aHelpPor := {"Nome do Responsável pelo Documento."}                      
aHelpSpa := {"Nombre del responsable por el documento."}
aHelpEng := {"Name of Responsible for the Document."}
PutSx1(cPerg,"07","Nome do Responsável","Nombre del responsable","" ,"MV_CH7","C",50, 0 ,0 ,"G","","","","S","MV_PAR07","","","","","","","","","","","","","","","","",aHelpPor ,aHelpEng ,aHelpSpa)
PutSX1Help("P"+".MTR103A07.",aHelpPor,aHelpEng,aHelpSpa,.T.)

RestArea(aArea)
Return NIL   

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³M103RTFin ºAutor  ³ Pedro Pereira Lima º Data ³  27/04/12   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function M103RTFin()
Local cAliasQry := GetNextAlias()
Local aParc := {}
Local nX := 0
Local aRet := {}
Local aRetLin := {}
Local nTotParc := 0
Local nValParc := 0

aParc := Condicao((cAliasReg)->VALOR,(cAliasReg)->COND,,StoD((cAliasReg)->EMISSAO))

If MV_PAR01 == 1
	If Len(aParc) > 1 //Existem parcelas para a NF em questão
		cParc := "A" //Primeira parcela
		For nX := 1 To Len(aParc)
			DbSelectArea("SE2")
			DbSetOrder(1)
			If DbSeek(xFilial("SE2")+(cAliasReg)->(PREFIXO+DUPLIC)+cParc+SubStr((cAliasReg)->ESPECIE,1,TamSX3("E2_TIPO")[1])+(cAliasReg)->(CLIFOR+LOJA))
				DbSelectArea("SEK")
				DbSetOrder(1)
				If DbSeek(xFilial("SEK")+SE2->(E2_ORDPAGO+"TB"+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO))
					
					BeginSql Alias cAliasQry
						SELECT EK_NUM RECIBO, EK_TIPO TIPODOC, EK_VLMOED1 VALOR,;
								( SELECT SUM(EK_VLMOED1)
								  FROM %TABLE:SEK% SEK
								  WHERE EK_FILIAL = %EXP:xFilial("SEK")% AND
								  EK_ORDPAGO = %EXP:SEK->EK_ORDPAGO% AND
								  EK_TIPO = %EXP:SE2->E2_TIPO% AND
								  EK_TIPODOC = %EXP:"TB"% AND
								  EK_EMISSAO BETWEEN %EXP:DtoS(MV_PAR03)% AND %EXP:DtoS(MV_PAR04)% AND
								  (( EK_MOEDA = 1 AND EK_VALORIG >= 50000 ) OR ( EK_MOEDA = 2 AND (EK_VALORIG*EK_TXMOE02) >= 50000 )) AND
								  SEK.%NotDel% ) VALORIG,;
							   EK_BANCO CODBCO, EK_AGENCIA AGENCIA, EK_CONTA CONTA, EK_EMISSAO EMISSAO
						  FROM %TABLE:SEK% SEK
						 WHERE EK_FILIAL = %EXP:xFilial("SEK")% AND
						       EK_ORDPAGO = %EXP:SEK->EK_ORDPAGO% AND
						       EK_TIPODOC <> %EXP:"TB"% AND
						       EK_TIPO <> %EXP:SE2->E2_TIPO% AND
						       EK_SALDO <> EK_VALOR AND
						       SEK.%NotDel%
						ORDER BY EK_SEQ
					EndSql
					
					(cAliasQry)->(DbGoTop())
					While !(cAliasQry)->(Eof())
						cBanco := (cAliasQry)->(CODBCO+AGENCIA+CONTA)

						// Verificacao para nao demonstrar o valor pago maior do que o valor designado para a parcela (pode ocorrer quando houver um PA em dolar)
						If (cAliasQry)->VALOR > (cAliasQry)->VALORIG
							nValParc := (cAliasQry)->VALORIG
						Else
							nValParc := (cAliasQry)->VALOR
						EndIf

						nTotParc += nValParc
						
						SA6->(DbSetOrder(1))
						SA6->(DbSeek(xFilial("SA6")+cBanco))
						If StoD((cAliasQry)->EMISSAO) >= MV_PAR03 .And. StoD((cAliasQry)->EMISSAO) <= MV_PAR04
							aRetLin := {(cAliasQry)->CONTA,nValParc,nTotParc,SA6->A6_CGC,(cAliasQry)->RECIBO,AllTrim((cAliasQry)->TIPODOC),(cAliasQry)->EMISSAO}
							aAdd(aRet,aRetLin)
						EndIf
						
						(cAliasQry)->(dbSkip())
					EndDo
					(cAliasQry)->(DbCloseArea())

				Else
					cParc := AllTrim(Soma1(cParc))
					Loop
				EndIf
			EndIf
			cParc := AllTrim(Soma1(cParc))
		Next nX
	Else
		DbSelectArea("SE2")
		DbSetOrder(1)
		If DbSeek(xFilial("SE2")+(cAliasReg)->(PREFIXO+DUPLIC)+Space(TamSX3("E1_PARCELA")[1])+SubStr((cAliasReg)->ESPECIE,1,TamSX3("E2_TIPO")[1])+(cAliasReg)->(CLIFOR+LOJA))
			If !Empty("E2_ORDPAGO").And. E2_ORDPAGO != Replicate(" ", Len(E2_ORDPAGO))
				DbSelectArea("SEK")
				DbSetOrder(1)
				DbSeek(xFilial("SEK")+SE2->(E2_ORDPAGO+"TB"+E2_PREFIXO+E2_NUM+E2_PARCELA+E2_TIPO))
			
				cAliasQry := GetNextAlias()
				BeginSql Alias cAliasQry
					SELECT  EK_NUM RECIBO, EK_TIPO TIPODOC,;
							( SELECT SUM(EK_VLMOED1)
							  FROM %TABLE:SEK% SEK
							  WHERE EK_FILIAL = %EXP:xFilial("SEK")% AND
							  EK_ORDPAGO = %EXP:SEK->EK_ORDPAGO% AND
							  EK_TIPO = %EXP:SE2->E2_TIPO% AND
							  EK_TIPODOC = %EXP:"TB"% AND
							  EK_EMISSAO BETWEEN %EXP:DtoS(MV_PAR03)% AND %EXP:DtoS(MV_PAR04)% AND
							  (( EK_MOEDA = 1 AND EK_VALORIG >= 50000 ) OR ( EK_MOEDA = 2 AND (EK_VALORIG*EK_TXMOE02) >= 50000 )) AND
							  SEK.%NotDel% ) VALOR,;
							EK_BANCO CODBCO, EK_AGENCIA AGENCIA, EK_CONTA CONTA, EK_EMISSAO EMISSAO
					FROM %TABLE:SEK% SEK
					WHERE EK_FILIAL = %EXP:xFilial("SEK")% AND
					EK_ORDPAGO = %EXP:SEK->EK_ORDPAGO% AND
					EK_TIPO <> %EXP:SE2->E2_TIPO% AND
					EK_TIPODOC <> %EXP:"TB"% AND
					EK_EMISSAO BETWEEN %EXP:DtoS(MV_PAR03)% AND %EXP:DtoS(MV_PAR04)% AND
					EK_SALDO <> EK_VALOR AND
					SEK.%NotDel%
				EndSql
			
				(cAliasQry)->(DbGoTop())
			
				cBanco := (cAliasQry)->(CODBCO+AGENCIA+CONTA)
			
				SA6->(DbSetOrder(1))
				SA6->(DbSeek(xFilial("SA6")+cBanco))
				
				If StoD((cAliasQry)->EMISSAO) >= MV_PAR03 .And. StoD((cAliasQry)->EMISSAO) <= MV_PAR04
					aRetLin := {(cAliasQry)->CONTA,(cAliasQry)->VALOR,0,SA6->A6_CGC,(cAliasQry)->RECIBO,AllTrim((cAliasQry)->TIPODOC),(cAliasQry)->EMISSAO}
					aAdd(aRet,aRetLin)
				EndIf

				(cAliasQry)->(DbCloseArea())
			EndIf
		EndIf
	EndIf
Else
	If Len(aParc) > 1 //Existem parcelas para a NF em questão
		cParc := "A" //Primeira parcela
		For nX := 1 To Len(aParc)
			DbSelectArea("SE1")
			DbSetOrder(1)
			If DbSeek(xFilial("SE1")+(cAliasReg)->(PREFIXO+DUPLIC)+cParc+SubStr((cAliasReg)->ESPECIE,1,TamSX3("E2_TIPO")[1]))
				DbSelectArea("SEL")
				DbSetOrder(1)
				If DbSeek(xFilial("SEL")+SE1->(E1_RECIBO+"TB"+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO))
					
					cAliasQry := GetNextAlias()
					BeginSql Alias cAliasQry
						SELECT EL_NUMERO RECIBO, EL_TIPO TIPODOC, EL_VALOR VALOR, EL_BANCO CODBCO, EL_AGENCIA AGENCIA, EL_CONTA CONTA, EL_EMISSAO EMISSAO,
						EL_BCOCHQ BCOCHQ, EL_AGECHQ AGECHQ, EL_CTACHQ CTACHQ, EL_NITBCO NITBCO
						FROM %TABLE:SEL% SEL
						WHERE EL_FILIAL = %EXP:xFilial("SEL")% AND
						EL_RECIBO = %EXP:SEL->EL_RECIBO% AND
						EL_TIPODOC <> %EXP:"TB"% AND
						EL_TIPO <> %EXP:SE1->E1_TIPO% AND
						EL_EMISSAO BETWEEN %EXP:DtoS(MV_PAR03)% AND %EXP:DtoS(MV_PAR04)% AND
						SEL.%NotDel%
					EndSql
					
					(cAliasQry)->(DbGoTop())
					
					While !(cAliasQry)->(Eof())
						
						nTotParc += (cAliasQry)->VALOR
						
						If StoD((cAliasQry)->EMISSAO) >= MV_PAR03 .And. StoD((cAliasQry)->EMISSAO) <= MV_PAR04
							aRetLin := {IIf(Empty((cAliasQry)->CTACHQ),(cAliasQry)->CONTA,(cAliasQry)->CTACHQ),(cAliasQry)->VALOR,nTotParc,(cAliasQry)->NITBCO,(cAliasQry)->RECIBO,AllTrim((cAliasQry)->TIPODOC),(cAliasQry)->EMISSAO}
							aAdd(aRet,aRetLin)
						EndIf
						(cAliasQry)->(dbSkip())
					EndDo
					
					(cAliasQry)->(DbCloseArea())
				Else
					cParc := AllTrim(Soma1(cParc))
					Loop
				EndIf
			EndIf
			cParc := AllTrim(Soma1(cParc))
		Next nX
	Else
		DbSelectArea("SE1")
		DbSetOrder(1)
		If DbSeek(xFilial("SE1")+(cAliasReg)->(PREFIXO+DUPLIC)+Space(TamSX3("E1_PARCELA")[1])+(cAliasReg)->ESPECIE)
			If SE1->E1_STATUS != "A"
				DbSelectArea("SEL")
				DbSetOrder(2)
				DbSeek(xFilial("SEL")+SE1->(E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO+E1_CLIENTE+E1_LOJA))
			
				cAliasQry := GetNextAlias()
				BeginSql Alias cAliasQry
					SELECT EL_NUMERO RECIBO, EL_TIPODOC TIPODOC, EL_VALOR VALOR, EL_BANCO CODBCO, EL_AGENCIA AGENCIA, EL_CONTA CONTA, EL_EMISSAO EMISSAO,
					EL_BCOCHQ BCOCHQ, EL_AGECHQ AGECHQ, EL_CTACHQ CTACHQ , EL_NITBCO NITBCO
					FROM %TABLE:SEL% SEL
					WHERE EL_FILIAL = %EXP:xFilial("SEL")% AND
					EL_RECIBO = %EXP:SEL->EL_RECIBO% AND
					EL_TIPO <> %EXP:SE1->E1_TIPO% AND
					EL_EMISSAO BETWEEN %EXP:DtoS(MV_PAR03)% AND %EXP:DtoS(MV_PAR04)% AND
					SEL.%NotDel%
				EndSql
			
				(cAliasQry)->(DbGoTop())
				If StoD((cAliasQry)->EMISSAO) >= MV_PAR03 .And. StoD((cAliasQry)->EMISSAO) <= MV_PAR04
					aRetLin := {IIf(Empty((cAliasQry)->CTACHQ),(cAliasQry)->CONTA,(cAliasQry)->CTACHQ),(cAliasQry)->VALOR,0,(cAliasQry)->NITBCO,(cAliasQry)->RECIBO,AllTrim((cAliasQry)->TIPODOC),(cAliasQry)->EMISSAO}
					aAdd(aRet,aRetLin)
				EndIf	

				(cAliasQry)->(DbCloseArea())
			EndIf
		EndIf
	EndIf
EndIf

Conout( len( aRet ) )

Return aRet
